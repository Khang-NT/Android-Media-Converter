buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def keyStorePass = hasProperty("key_store_password") ? key_store_password : ""

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.github.khangnt.mcp"
        minSdkVersion 23
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "0.9.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            keyAlias 'MediaConverter'
            keyPassword keyStorePass
            storeFile file('release_key.jks')
            storePassword keyStorePass
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }

        release {
            signingConfig signingConfigs.release

            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    final _versionCode = 28

    flavorDimensions 'cpuArch'
    productFlavors {
        arm {
            dimension 'cpuArch'
            ndk {
                abiFilter 'armeabi'
            }

            versionCode 0 + _versionCode
            versionNameSuffix "-arm"
        }
        arm7 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'armeabi-v7a'
            }

            versionCode 10000 + _versionCode
            versionNameSuffix "-arm7"
        }
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }

            versionCode 20000 + _versionCode
            versionNameSuffix "-arm8"
        }
        x86 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'x86'
            }

            versionCode 30000 + _versionCode
            versionNameSuffix "-x86"
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'x86_64'
            }

            versionCode 40000 + _versionCode
            versionNameSuffix "-x86_64"
        }
    }

    lintOptions {
        ignore 'ExtraTranslation', 'MissingTranslation'
    }

    sourceSets {
        main {
            jni.srcDirs = []
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    final supportLibVersion = '1.0.0'
    implementation "androidx.appcompat:appcompat:$supportLibVersion"
    implementation "androidx.legacy:legacy-support-v4:$supportLibVersion"
    implementation "androidx.media:media:$supportLibVersion"
    implementation "androidx.cardview:cardview:$supportLibVersion"
    implementation "com.google.android.material:material:$supportLibVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0-beta01'

    final androidLifecycleExtensionVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidLifecycleExtensionVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$androidLifecycleExtensionVersion"

    final roomVersion = "2.1.0"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"

    implementation 'com.google.android.gms:play-services-ads:18.3.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }

    implementation 'com.liulishuo.filedownloader:library:1.7.1'

    implementation 'cat.ereza:customactivityoncrash:2.2.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply from: 'publish.gradle'

publishConfig {
    packageName = 'com.github.khangnt.mcp'
    track = 'beta'
    apkFiles = files(
            "$buildDir/outputs/apk/arm/release/app-arm-release.apk",
            "$buildDir/outputs/apk/arm7/release/app-arm7-release.apk",
            "$buildDir/outputs/apk/arm8/release/app-arm8-release.apk",
            "$buildDir/outputs/apk/x86/release/app-x86-release.apk",
            "$buildDir/outputs/apk/x86_64/release/app-x86_64-release.apk"
    )
    mappings = files(
            "$buildDir/outputs/mapping/arm/release/mapping.txt",
            "$buildDir/outputs/mapping/arm7/release/mapping.txt",
            "$buildDir/outputs/mapping/arm8/release/mapping.txt",
            "$buildDir/outputs/mapping/x86/release/mapping.txt",
            "$buildDir/outputs/mapping/x86_64/release/mapping.txt"
    )
    serviceAccountJson = 'app/service_account.json'
}

afterEvaluate {
    tasks.create("testReleaseUnitTest").dependsOn(
            "testArmReleaseUnitTest",
            "testArm7ReleaseUnitTest",
            "testArm8ReleaseUnitTest",
            "testX86ReleaseUnitTest",
            "testX86_64ReleaseUnitTest"
    )
    tasks.create("lintRelease").dependsOn(
            "lintArmRelease",
            "lintArm7Release",
            "lintArm8Release",
            "lintX86Release",
            "lintX86_64Release"
    )
}

tasks.findByName("publishApks").dependsOn("assembleRelease")